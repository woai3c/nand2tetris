/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(24576);
    }

    /**                             
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c, temp;

        do Output.printChar(0);

        while (true) {
            let temp = Keyboard.keyPressed();

            if (temp > 0) {
                let c = temp;
                while (true) {
                    let temp = Keyboard.keyPressed();
                    if (temp = 0) {
                        do Output.backSpace();
                        if (~(c = 129) & (~(c = 128))) {
                            do Output.printChar(c);
                        }
                        return c;
                    }
                }
            }
        }
        return 0;
    }

    /**                             
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;

        do Output.printString(message);
        let s = String.new(80);

        while (true) {
            let c = Keyboard.readChar();

            if (c = 128) {
                do Output.println();
                return s;
            }

            if (c = 129) {
                do s.eraseLastChar();
                do Output.backSpace();
            } else {
                let s = s.appendChar(c);
            }
        }
        return 0;
    }   

    /**                             
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String s;

        let s = Keyboard.readLine(message);
        return s.intValue();
    }
}